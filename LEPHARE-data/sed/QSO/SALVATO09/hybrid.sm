

#
#hybrid
hybrid

     erase expand 1.3 lweight 3 ltype 0 ctype black
     limits $(lg(700)) $(lg(160000)) $(lg(0.001)) $(lg(9.5)) box 

     define print_noheader 2

     ctype green sed_sub  TQSO1_template_norm.sed
     ctype red sed_sub    I22491_template_norm.sed
     ctype black

     ltype 0 lweight 1
     #loop on fraction
     do j=0,10,0.5 {
         #print fraction
         echo  $($j*10.)
         #Cree la SED
         define nameout  ('hybrid_gal'+'$($j*10.)'+'_agn'+'$(100.-$j*10.)'+'.sed')
         echo $nameout
         trans_1sed $($j*10.) $nameout
         ##plot la SED
         sed_sub  $nameout 
         ##Ecrit la liste d'output
         define nom  ('hybrid_gal'+'$($j*10.)'+'_agn'+'$(100.-$j*10.)'+'.sed  ')
         set bidon = < $nom >
         print + HYBRID_MOD.list  {bidon}
      }

      ctype black
      expand 1.3 lweight 3 ltype 0 
      xlabel log (\lambda (\AA))
      ylabel flux (without unity)
      
end


#
# factor
trans_1sed 2

     #first base of lambda
     #set l2=0,800000,20
     set l2=0,300000,20

     #add emmission lines
     data TQSO1_template_norm.sed read {lq 1 fq 2}
     #Interp emmision lines in the final lambda frame frame
     interp2 lq fq l2 fqinterp

     #Read optical part
     data I22491_template_norm.sed  read {lga 1 fga 2}
     #Interp emmision lines in the final lambda frame frame
     interp2 lga fga l2 fginterp

     #Renormalise
     set fgbid=fginterp if(l2>100 && l2<1000000)
     set fqbid=fqinterp if(l2>100 && l2<1000000)
     set fgrenorm=fginterp/$(sum(fgbid))
     set fqrenorm=fqinterp/$(sum(fqbid))

     #Apply the coeff
     set fqcoef=fqrenorm*$(100-$1)
     set fgcoef=fgrenorm*$1
     #Sum
     set fhybrid=fqcoef+fgcoef

     #output
     define print_noheader 2
     print $2 '%20.10f %20.10f \n' {l2 fhybrid}


     delete _index delete _x1 delete _y1 delete _x2 delete _y2 

end


sed_sub      1
              data $1 read {l 1 f 2}
              set bid=f if(l>100 && l<1000000)
              vecminmax bid min max
              connect (lg(l)) (lg(f/$max)) 
end

